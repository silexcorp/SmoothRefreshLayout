// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        google()
    }
}

subprojects {
    if (it.name != 'app') {
        apply plugin: 'com.diffplug.gradle.spotless'
        spotless {
            java {
                target fileTree('.') {
                    include '**/*.java'
                    exclude '**/MaterialProgressDrawable.java'
                }
                googleJavaFormat('1.7').aosp()
                licenseHeaderFile project.rootProject.file('spotless/copyright.java')
            }
        }
    } else {
        apply plugin: 'com.diffplug.gradle.spotless'
        spotless {
            java {
                target '**/*.java'
                googleJavaFormat('1.7').aosp()
            }
        }
    }
}

ext {
    android = [
            compileSdkVersion: 28,
            buildToolsVersion: "28.0.3",
            minSdkVersion    : 11,
            targetSdkVersion : 28,
            versionCode      : 67,
            versionName      : "1.6.6.8"
    ]
    libs = [
            recyclerview: "com.android.support:recyclerview-v7:28.0.0",
            design      : "com.android.support:design:28.0.0",
            appcompatV7 : "com.android.support:appcompat-v7:28.0.0",
            supportV13  : "com.android.support:support-v13:28.0.0"
    ]
    bintray = [
            siteUrl       : 'https://github.com/dkzwm/SmoothRefreshLayout',
            gitUrl        : 'https://github.com/dkzwm/SmoothRefreshLayout.git',
            groupId       : "me.dkzwm.widget.srl",
            libVersion    : android.versionName,
            developerId   : "dkzwm",
            developerName : "Zhang Weiming",
            developerEmail: "aidkzwm@gmail.com",
            licenseName   : "MIT License",
            licenseUrl    : "https://opensource.org/licenses/MIT",
    ]
    allLicenses = ['MIT']
}

